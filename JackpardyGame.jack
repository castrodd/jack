class JackpardyGame {
   field Board board; // the board of this game
   field Questions questions; // the question checker
   field char currentCategory;
   field char currentValue;

   /** Constructs a new Jackpardy Game. */
   constructor JackpardyGame new() {
      // Creates a new home screen with board, initial score, and question prompt
      let board = Board.new();
      let questions = Questions.new();
      let currentCategory = 0;
      let currentValue = 0;
      return this;
   }

   /** Disposes this game. */
   method void dispose() {
      do board.dispose();
      do questions.dispose();
      do Memory.deAlloc(this);
      return;
   }

   /** Runs the game */
   method void run() {
      var int remainingQuestions;
      let remainingQuestions = board.showRemainingQuestions();
      
      while (remainingQuestions > 0) {
        do board.display();
        
        do questions.askCategory();
        while (currentCategory = 0) {
            let currentCategory = Keyboard.readChar();
        }

        do questions.askValue();
        while (currentValue = 0) {
            let currentValue = Keyboard.readChar();
        }

        do questions.validateAndShowQuestion(currentCategory, currentValue);

        let currentCategory = 0;
        let currentValue = 0;
        let remainingQuestions = remainingQuestions - 1;
      }

      do Screen.clearScreen();
      do Output.moveCursor(13, 25);
      do Output.printString("Game Over!");
      return;
   }
}